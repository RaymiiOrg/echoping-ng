dnl $Id$

dnl Process this file with autoconf to produce a configure script.
AC_INIT(echoping.h)
AM_INIT_AUTOMAKE(echoping, 3.0.1)
AM_CONFIG_HEADER(config.h)

dnl User options
AC_ARG_ENABLE(http,
 [--enable-http   HTTP (Web's main protocol) support],dnl
 [if test "$enableval" = "yes"; then
 AC_DEFINE(HTTP)
 HTTP=1
 fi],
 dnl Default: enable it
 [AC_DEFINE(HTTP)
  HTTP=1])
AC_ARG_ENABLE(icp,
 [--enable-icp   ICP (for testing Web proxies/caches) support],dnl
 [if test "$enableval" = "yes"; then
 AC_DEFINE(ICP)
 fi])
dnl See T/TCP later 

dnl Checks for programs.
AC_PROG_CC
if test "$GCC" = yes; then
        CFLAGS="$CFLAGS -Wall"
fi
AC_PROG_INSTALL

dnl Checks for libraries.

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(sys/time.h unistd.h)
CF_SYS_ERRLIST

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_HEADER_TIME

dnl Checks for library functions.
CF_LIB_SOCKET
CF_LIB_NSL
AC_TYPE_SIGNAL
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(gettimeofday socket gethostbyname sigaction, , AC_MSG_ERROR(Missing mandatory function))

dnl T/TCP
AC_MSG_CHECKING([T/TCP])
AC_TRY_COMPILE(
 [#include <sys/types.h>
  #include <sys/socket.h>
 ],
 [int foobar = MSG_EOF;],
 [AC_DEFINE(TTCP)
  ac_have_ttcp="yes"],
 ac_have_ttcp=no)
AC_MSG_RESULT($ac_have_ttcp)
AC_ARG_ENABLE(ttcp,
 [--enable-ttcp   T/TCP (Transaction TCP) support],
 [if test "$enableval" = "yes"; then
   if test $ac_have_ttcp = "yes"; then
     AC_DEFINE(HAVE_TTCP)
   else
     AC_MSG_WARN([No T/TCP support on this system, request ignored])
   fi
  fi],
 dnl Default: enable it if supported
 if test $ac_have_ttcp = "yes"; then
   AC_DEFINE(HAVE_TTCP)
 fi)

if test "$HTTP" = "1"; then
CF_CHECK_SERVICES
fi

AC_OUTPUT(Makefile)


